%import common.ESCAPED_STRING
// %import common.STRING
%import common.INT
%import common.FLOAT
%import common.BOOL
%import common.NEWLINE      -> _NL
%import common.WS_INLINE
%import common.CNAME
%import common.SIGNED_NUMBER

%ignore WS_INLINE

?start: frontmatter [ body ]

?frontmatter.1: "---" " "* _NL [ yaml_block ] "---" " "* _NL
yaml_block: (kvpair _NL)+
kvpair: PROPERTY ":" value? 
?value: array
     | simple_value
?array: (inline_seq | indented_seq)
inline_seq: "[" simple_value ( "," simple_value )* "]"
indented_seq:  ( _NL _ARRAY_ITEM_MARKER simple_value )+
?simple_value.2: STRING | BOOLEAN | NUMBER | DATETIME

!body: (TEXT_LINE | BLANK_LINE)*

TEXT_LINE.2: LINE_OF_TEXT _NL
BLANK_LINE.1: (" "+) _NL

PROPERTY: CNAME | KEY_NAME
KEY_NAME: /^\s*(?=.*\:)([^:]+)/
DATETIME.5: /\d{1,4}-\d{1,4}-\d{1,4}(\s*[\sTt]?\s*\d\d\:\d\d(\:\d\d)?)?/
NUMBER.4: SIGNED_NUMBER
BOOLEAN.3: "true"i | "false"i
STRING.1: WS_INLINE /(.+)$/m
_ARRAY_ITEM_MARKER: /^\s*(?<!-)-(?!-{1,})/m
LINE_OF_TEXT.2: /^(?=.*\S)[^\n]+/m

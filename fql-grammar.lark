%import common.WS
%ignore WS
%import common.LETTER
%import common.CNAME
%import common.ESCAPED_STRING
%import common.NUMBER           -> CNUM

// Todo: update_stmt | delete_stmt | cache_stmt
?start: cd_stmt | quit_stmt | select_stmt | insert_stmt

cd_stmt: "CHANGE"i (path | quoted_path) ";"?
quit_stmt: "QUIT"i ";"?
select_stmt: "SELECT"i properties "FROM"i quoted_resource_path [ RECURSE_FLAG ] [ where_clause ] [ sort_clause ] ";"?
insert_stmt: "INSERT"i key_value_object "INTO"i files [ IF_EXISTS_FLAG ] [ where_clause ] ";"?

properties: ASTERISK | PROPERTY ("," PROPERTY)+
sort_clause: "SORT BY"i (PROPERTY ",")* PROPERTY [ ORDER ] ";"?
// where_clause: "WHERE"i PROPERTY OPERATOR simple_value 
//             | "WHERE"i PROPERTY ARRAY_OPERATOR array
where_clause: "WHERE"i expression

expression: PROPERTY "=" simple_value                  -> eq
          | PROPERTY "<>" simple_value                 -> neq
          | PROPERTY "!=" simple_value                 -> neq
          | PROPERTY "<" simple_value                  -> lt
          | PROPERTY ">" simple_value                  -> gt
          | PROPERTY "<=" simple_value                 -> lte
          | PROPERTY ">=" simple_value                 -> gte
          | PROPERTY "FUZZY MATCH"i STRING             -> fuzzy_match
          | PROPERTY ["REGEX"i] "MATCH"i STRING        -> regex_match
          | PROPERTY ["REGEX"i] "NO MATCH"i STRING     -> regex_no_match
          | PROPERTY "IN"i array                       -> in_list
          | PROPERTY "NOT IN"i array                   -> notin_list

?quoted_resource_path: "'" resource_path "'" | "\"" resource_path "\""
resource_path: path [ WILDCARD_TEXT ] 
?quoted_path: "'" path "'" | "\"" path "\""
path: _absolute_path | _relative_path | _path_with_environment_variable
files: path WILDCARD_TEXT | "'" path WILDCARD_TEXT "'" | "\"" path WILDCARD_TEXT "\""

!_path_with_environment_variable: "$" CNAME ("\\" | "/") (DIRECTORY ("\\" | "/"))*
// _relative_path: ("."|"..") ["\\" | "/"] (DIRECTORY ("\\" | "/"))*
!_relative_path: ("."|"..") (("\\" | "/") DIRECTORY)* ["\\" | "/"]
_absolute_path: _windows_path | _linux_path
!_windows_path: LETTER ":\\" (DIRECTORY "\\")*
!_linux_path: "/" (DIRECTORY "/")*


// key_value_object: "{" [pair] ("," pair)* "}"
key_value_object: "{" (pair ",")* [pair] "}"

pair: PROPERTY ":" value 

value: array | template_variable | simple_value
// value: STRING
//      | NUMBER
//      | array
//      | BOOLEAN
//      | DATETIME
//      | NULL
//      | template_variable

// OPERATOR: "=" | "<>" | "!=" | "<" | ">" | ">=" | "<=" 
//         | "FUZZY MATCH"i 
//         | ["REGEX"i] "MATCH"i
// ARRAY_OPERATOR: "IN"i | "NOT IN"i

array: "[" [ simple_value ("," simple_value)* ] "]"
simple_value: NUMBER | STRING | BOOLEAN | DATETIME | NULL
template_variable: "{{" VARIABLE_STRING "}}"

ORDER: "ASC"i | "DESC"i
RECURSE_FLAG: "RECURSE"i
IF_EXISTS_FLAG: "IF EXISTS"i

DATETIME.5: _DATETIME_PATTERN
_DATETIME_PATTERN: /\d{1,4}-\d{1,4}-\d{1,4}(\s*[\sTt]?\s*\d\d\:\d\d(\:\d\d)?)?/
NUMBER.3: CNUM
BOOLEAN.2: "true"i | "false"i
STRING: ESCAPED_STRING
NULL.2: "null"

ASTERISK: "*"
PROPERTY: CNAME | ESCAPED_STRING
DIRECTORY: /[^\/\\]+/
VARIABLE_STRING: /(.+)(?=\}\})/ 
WILDCARD_TEXT: /[\][()_\-?*\.\w\s]*\.\w*/ | "*" | "?"